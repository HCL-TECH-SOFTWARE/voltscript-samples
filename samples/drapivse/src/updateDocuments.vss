%REM
	Copyright 2023 HCL America, Inc.
	Licensed under the Apache License, Version 2.0 (the "License"); 
	you may not use this file except in compliance with the License. 
	You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

	Unless required by applicable law or agreed to in writing, 
	software distributed under the License is distributed on an "AS IS" BASIS, 
	WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
	See the License for the specific language governing permissions and limitations under the License	
%END REM

Option Declare
Option Public

Use "../libs/functions"

Sub Initialize
    Dim server as New DrapiServer
    Dim request as DrapiRequest, response as DrapiResponse
    Dim parser as New JsonParser(), jsonObj as JsonObject
    Dim replaceItems as New JsonObject()
    Dim query as String, email as String

    Try
        server.serverURL = SERVER_NAME
        Call server.login(USERNAME, USERPASSWORD)
        Set request = server.createRequest("demo")
        Call replaceItems.insertValue("Color", "Maroon")
        query = "form = 'Customer' and Color = 'Red'"

        Set response = request.bulkPatchDocuments(query, replaceItems.toString(False), "html", "default", 5000, True, True)
        If response.ResponseCode = 200 Then
            Call parser.loadFromJSON(response.ContentBody)
            Set jsonObj = parser.getRootObject()
            If (jsonObj.IsArray) Then
                Print jsonObj.ChildCount & " documents updated:"
                ForAll child in jsonObj.getChildren()
                    Print "Updated UNID: " & CStr(child.getDescendantPath("@meta/unid").scalarValue)
                    If child.isChild("email") Then
                        email = CStr(child.getChild("email").scalarValue)
                    Else
                        email = "No email found"
                    End If
                    Print "     Email: " & email
                    Print "     New Color: " & CStr(child.getChild("Color").scalarValue)

                End ForAll
            Else
                Print "No documents updated."
            End If
        Else
            Print "Error updating documents: " & response.ResponseCode & " - " & response.ErrorMessage
        End If

    Catch
        Print "Error " & Error() & " on line " & Erl()
    Finally
        If (Not server is Nothing && server.connected) Then server.logout
    End Try
End Sub