Option Declare
Option Public

Use "../libs/VoltMXObjects"
Use "../libs/functions"
UseVSE "*ContextVSE"
UseVSE "*DrapiVSE"

Sub Initialize
    Dim ctx as New Context()
    Dim data as String
    Dim request as DrapiRequest
    Dim doc as DrapiDocument
    Dim companyPayload as JsonObject

    Dim parser as New JsonParser()
    Dim server as New DrapiServer()
    Dim replaceItems as New JsonObject()

    Dim response as DrapiResponse
    Dim newCompanyName As String
    Dim oldCompanyName As String
    Dim query as String
    Dim unid as String

    Dim username as String 
    Dim password as String

    Try
        data = ctx.Context
        Call extractObjects(data, false)

        '****************
        'START OF FOUNDRY HANDLER
        '****************
        unid = VoltMXRequest.getInputParam("unid")
        newCompanyName = VoltMXRequest.getInputParam("CompanyName")
        server.serverURL = VoltMXRequest.getInputParam("serverURL")
        username = VoltMXRequest.getInputParam("username")
        password = VoltMXRequest.getInputParam("password")
        Call server.login(username, password)

        Set request = server.createRequest("foundrycontacts")
        Set doc = request.getDocument(unid)

        Call parser.loadFromJson(doc.JsonValue)
        Set companyPayload = parser.getRootObject()
        oldCompanyName = companyPayload.getChild("Company").scalarValue
        Call companyPayload.insertValue("Company", newCompanyName)
        doc.JsonValue = companyPayload.toString(false)
        Call VoltMxResult.result.insertValue("companyUpdated", doc.put("", "default"))

        query = "Form = 'Contact' and Company = '" & oldCompanyName & "'"
        Call replaceItems.insertValue("Company", newCompanyName)
        Set response = request.bulkPatchDocuments(query, replaceItems.toString(false), "html", "default", 5000, True, True)
        Call parser.loadFromJson(response.ContentBody)
        VoltMxResult.httpStatusCode = 200
        Call VoltMxResult.result.insertValue("contactsUpdated", parser.getRootObject())

        '****************
        'END OF FOUNDRY HANDLER
        '****************

    Catch
        Call VoltMXResult.setErrorMessage(getErrorMsg("VOLTSCRIPT_CONNECTOR: "))
    Finally
        Print VoltMxResult.toJson().toString(false)
    End Try
End Sub

Private Function getErrorMsg(prefix as String) as String
    If (Instr(Error(), "line ") > 0) Then
        Return Error()
    End If
    Return prefix & " ERROR: " & Error() & " (" & Err() & "), line " & Erl()
End Function